block(`Isis_L2_rules) {
   alias_all(`libbatfish:GeneratedRoute),
   alias_all(`libbatfish:Interface),
   alias_all(`libbatfish:Ip),
   alias_all(`libbatfish:Isis),
   alias_all(`libbatfish:Layer2),
   alias_all(`libbatfish:Node),
   alias_all(`libbatfish:PolicyMap),
   alias_all(`libbatfish:Route),
   clauses(`{
// BEGIN CODE

BestIsisL2Route(route),
   IsisL2Network(node, network)
<-
   IsisL2Route(route),
   Route_cost[route] = cost,
   Route_network[route] = network,
   Route_node[route] = node,
   MinIsisL2RouteCost[node, network] = cost,
   !IsisL1Network(node, network).

IsisL2EnabledInterface(node, interface) <-
   SetIsisL2ActiveInterface(node, interface) ;
   SetIsisL2PassiveInterface(node, interface).
   
IsisL2Neighbors(node1, int1, cost1, node2, int2, cost2) <-
   SetIsisL2Node(node1),
   SetIsisL2Node(node2),
   SetIsisArea(node1, _),
   SetIsisArea(node2, _),
   LanAdjacent(node1, int1, node2, int2),
   SetIsisInterfaceCost(node1, int1, cost1),
   SetIsisInterfaceCost(node2, int2, cost2),
   SetIsisL2ActiveInterface(node1, int1),
   SetIsisL2ActiveInterface(node2, int2).

// (base case)
IsisL2Route(route),
   Route(route),
   NonIsisExportableRoute(route),
   Route_constructor[node, network, nextHopIp, protocol] = route,
   Route_admin[route] = admin,
   Route_cost[route] = cost,
   Route_network[route] = network,
   Route_nextHopIp[route] = nextHopIp,
   Route_node[route] = node,
   Route_protocol[route] = protocol
<-
   !ConnectedRoute(node, network, _), // is this necessary?
   IsisL2Neighbors(node, _, nodeIntCost, nextHop, nextHopInt, _),
   IpReadyInt(nextHop, nextHopInt, nextHopIp, _),
   ConnectedRoute(nextHop, network, nextHopConnectedInt),
   IsisL2EnabledInterface(nextHop, nextHopConnectedInt),
   SetIsisInterfaceCost[nextHop, nextHopConnectedInt] = nextHopIntCost,
   cost = nodeIntCost + nextHopIntCost,
   protocol = RoutingProtocol_ISIS_L2[],
   SetNodeVendor(node, vendor),
   AdministrativeDistance[vendor, protocol] = admin.
// (recursive (forward L2 Routes)
IsisL2Route(route),
   Route(route),
   NonIsisExportableRoute(route),
   Route_constructor[node, network, nextHopIp, protocol] = route,
   Route_admin[route] = admin,
   Route_cost[route] = cost,
   Route_network[route] = network,
   Route_nextHopIp[route] = nextHopIp,
   Route_node[route] = node,
   Route_protocol[route] = protocol
<-
   !ConnectedRoute(node, network, _),
   IsisL2Neighbors(node, _, nodeIntCost, nextHop, nextHopInt, _),
   IpReadyInt(nextHop, nextHopInt, nextHopIp, _),
   IsisL2Neighbors(nextHop, _, _, secondHop, _, _),
   BestIsisL2Route(subRoute),
   Route_cost[subRoute] = subCost,
   Route_network[subRoute] = network,
   Route_nextHopIp[subRoute] = secondHopIp,
   Route_node[subRoute] = nextHop,
   HasIp(secondHop, secondHopIp),
   node != secondHop,
   cost = subCost + nodeIntCost,
   protocol = RoutingProtocol_ISIS_L2[],
   SetNodeVendor(node, vendor),
   AdministrativeDistance[vendor, protocol] = admin.

MinIsisL2RouteCost[node, network] = minCost <-
   agg<<minCost = min(cost)>>
      IsisL2Route(route),
      Route_cost[route] = cost,
      Route_network[route] = network,
      Route_node[route] = node.

lang:compiler:disableError:NEGATION_RECURSION[] = true.
lang:compiler:disableWarning:NEGATION_RECURSION[] = true.
lang:compiler:disableError:AGGREGATE_RECURSION[] = true.
lang:compiler:disableWarning:AGGREGATE_RECURSION[] = true.
lang:compiler:disableWarning:SPECIFIC_STARRED_EDGE_IN_SAFETY_GRAPH_CYCLE[] = true.
lang:compiler:error:ENTITY_CREATION_RECURSION[]=false.
lang:compiler:warning:ENTITY_CREATION_RECURSION[]=false.

// END CODE
   })
} <-- .
